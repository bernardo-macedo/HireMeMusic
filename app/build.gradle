apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply from: 'helpers.gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.bmacedo.hirememusic"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue 'string', "SPOTIFY_API_KEY", "\"${getSpotifyApiKey().trim()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kapt {
        correctErrorTypes = true
    }
}

ext {
    versions = [
            appCompat       : "1.1.0-alpha03",
            coreKtx         : "1.1.0-alpha05",
            constraintLayout: "1.1.3",
            navigation      : "2.0.0",
            lifecycle       : "2.1.0-alpha03",
            coroutines      : "1.1.1",
            dagger          : "2.20",
            spotifySdk      : "1.1.0",
            timber          : "4.7.1",
            jUnit           : "4.12",
            testRunner      : "1.1.2-alpha02",
            espresso        : "3.2.0-alpha02"
    ]
}

dependencies {
    // Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation "androidx.appcompat:appcompat:$versions.appCompat"
    implementation "androidx.core:core-ktx:$versions.coreKtx"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
    implementation "androidx.navigation:navigation-fragment:$versions.navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-runtime-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui:$versions.navigation"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"

    // Third Party SDK
    implementation "com.spotify.android:auth:$versions.spotifySdk"

    // Debugging libs
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Annotation processor libs
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

    // Unit Test
    testImplementation "junit:junit:$versions.jUnit"

    // Integration Test
    androidTestImplementation "androidx.test:runner:$versions.testRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}
